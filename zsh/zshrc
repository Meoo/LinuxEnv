# Variables for interactive
export GCC_COLORS='error=01;31:warning=01;35:note=01;36:caret=01;32:locus=01;35:quote=01'

# ls
eval "$(dircolors)"
alias ls='ls --color=auto -hp'
alias l='ls -l'
alias ll='ls -Al'
l.() { ls -Ald ${1:+${1}/}.* }

# Other aliases
alias rm='rm -I'
alias cp='cp -i'
alias mv='mv -i'
alias grep='grep --color=auto'
alias df='df -h'
alias du='du -h'
alias less='less -q'
alias ccat='highlight -O ansi --force -q'
cless(){ ccat $* | less -R }

# Binds
bindkey -e
bindkey '\e[1~' beginning-of-line
bindkey '\e[4~' end-of-line
bindkey '\e[2~' overwrite-mode
bindkey '\e[3~' delete-char

# History
HISTFILE=${ZDOTDIR:-$HOME}/.zhistory
HISTSIZE=10000
SAVEHIST=10000

setopt APPEND_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt HIST_NO_STORE
setopt HIST_VERIFY
setopt HIST_SAVE_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY

# cd = pushd
setopt AUTO_PUSHD
setopt PUSHD_TO_HOME
setopt PUSHD_IGNORE_DUPS
setopt PUSHD_SILENT
setopt PUSHD_MINUS

# Sanity
setopt NO_RM_STAR_SILENT
setopt NOMATCH
setopt PRINT_EXIT_VALUE

# Glob
setopt GLOB_COMPLETE
setopt NO_CASE_GLOB
setopt NUMERIC_GLOB_SORT
setopt EXTENDED_GLOB
setopt RC_EXPAND_PARAM

# Proc
setopt NO_BG_NICE
setopt CHECK_JOBS
setopt NO_HUP
setopt NOTIFY

# Misc
setopt NO_BEEP
setopt AUTO_CD
setopt CHASE_LINKS

# Prompt
if [[ -r ${ZDOTDIR:-$HOME}/.zshprompt ]]; then
  source ${ZDOTDIR:-$HOME}/.zshprompt
elif [[ -r /etc/zsh/zshprompt ]]; then
  source /etc/zsh/zshprompt
fi

# Zsh modules
autoload -Uz compinit promptinit
compinit
promptinit

# Completion
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "${ZDOTDIR:-$HOME}/.zcompcache"

zstyle ':completion:*' completer _expand _complete _ignored
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

## Case insensitive
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}'

## Group & menu
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '-> %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash yes

zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*:descriptions' format '%B%F{magenta}[%d]%f%b'

## Dirs
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'users' 'expand'
zstyle ':completion:*' squeeze-slashes true

## Ignore duplicates
zstyle ':completion:*:(rm|kill|diff):*' ignore-line other

## man
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.*' insert-sections true
